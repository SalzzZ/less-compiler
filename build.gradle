
import org.gradle.plugins.signing.Sign

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.github.townsfolk:gradle-release:1.2'
    classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.12'
}

// Sign release archives
ext.isRelease = !version.endsWith('SNAPSHOT')
ext.isUpload = false
gradle.taskGraph.whenReady { taskGraph ->
  ext.isUpload = gradle.taskGraph.hasTask(':uploadArchives') || gradle.taskGraph.hasTask(':uploadInternal')
  if (isRelease && isUpload && gradle.taskGraph.allTasks.any { it instanceof Sign }) { 
    def password = System.console().readPassword('\n\n\n===>  Signing key password: ')
    allprojects { ext.'signing.password' = password }
  }
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

def configurePom(def pom) {
  pom.project {
    name 'less-compiler'
    description 'LESS compiler based on less.js'
    url 'https://github.com/squarespace/squarespace-less'

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    scm {
      connection 'scm:git:git@github.com:squarespace/squarespace-less.git'
      url 'scm:git:git@github.com:squarespace/squarespace-less.git'
      developerConnection 'scm:git:git@github.com:squarespace/squarespace-less.git'
    }

    issueManagement {
      system 'github'
      url 'https://github.com/squarespace/squarespace-less/issues'
    }
  }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'license'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'
  apply plugin: 'signing'
  apply plugin: 'maven'
  apply plugin: 'release'

  group 'com.squarespace.less'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  license {
    header rootProject.file('conf/apache-2.0-header.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders true
    include '**/*.java'
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'com.beust:jcommander:1.30'

    testCompile 'org.testng:testng:6.8.7'
    testCompile 'com.googlecode.java-diff-utils:diffutils:1.3.0'
  }

  checkstyle {
    toolVersion = '5.7'
    ignoreFailures = false
    configFile = rootProject.file('checkstyle.xml')
  }

  test {
    useTestNG()
    finalizedBy jacocoTestReport
  }

  jacoco {
    toolVersion '0.7.1.201405082137'
  }

  jacocoTestReport {
    group = 'Reporting'
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
      html.destination = "${buildDir}/jacoco-report"
    }
  }

  eclipse.project {
    natures 'net.sf.eclipsecs.core.CheckstyleNature'
    buildCommand 'net.sf.eclipsecs.core.CheckstyleBuilder'
  }

  signing {
    required { isRelease && isUpload }
    sign configurations.archives
  }

  task testJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from sourceSets.test.output
  }

  task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives testJar
    archives javadocJar
  }


  uploadArchives {
    repositories.mavenDeployer {
      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        authentication(
          userName: System.getenv('OSS_USERNAME'),
          password: System.getenv('OSS_PASSWORD')
        )
      }

      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(
          userName: System.getenv('OSS_USERNAME'),
          password: System.getenv('OSS_PASSWORD')
        )
      }

      configurePom(pom)
    }
  }

  task uploadInternal(type: Upload) {
    description = 'Uploads archives to internal Nexus'
    configuration = configurations.archives

    repositories.mavenDeployer {
      repository(url: NEXUS_INTERNAL_RELEASE_URL)
      snapshotRepository(url: NEXUS_INTERNAL_SNAPSHOT_URL)
      configurePom(pom)
    }
  }

}

